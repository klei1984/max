include(versions)
include(FetchContent)

if(EXISTS ${PROJECT_SOURCE_DIR}/dependencies/${MINIUPNP_FILE})
	file(${MINIUPNP_HASH_TYPE} ${PROJECT_SOURCE_DIR}/dependencies/${MINIUPNP_FILE} MINIUPNP_FILE_HASH)

	if(${MINIUPNP_FILE_HASH} STREQUAL ${MINIUPNP_HASH})
		set(MINIUPNP_URI file://${PROJECT_SOURCE_DIR}/dependencies/${MINIUPNP_FILE})
	endif()
endif()

FetchContent_Declare(
	MINIUPNP
	TIMEOUT 60
	URL ${MINIUPNP_URI}
	URL_HASH ${MINIUPNP_HASH_TYPE}=${MINIUPNP_HASH}
	DOWNLOAD_EXTRACT_TIMESTAMP FALSE
	OVERRIDE_FIND_PACKAGE
	SOURCE_SUBDIR miniupnpc
)

if(NOT BUILD_SHARED_LIBS)
	set(UPNPC_BUILD_SHARED FALSE)
endif()

set(UPNPC_BUILD_TESTS FALSE)
set(UPNPC_BUILD_SAMPLE FALSE)
set(UPNPC_NO_INSTALL TRUE)

# Force Release build without debug info (treat as system library)
set(CMAKE_BUILD_TYPE_BACKUP ${CMAKE_BUILD_TYPE})
set(CMAKE_BUILD_TYPE Release)

FetchContent_MakeAvailable(MINIUPNP)

# Restore build configuration
set(CMAKE_BUILD_TYPE ${CMAKE_BUILD_TYPE_BACKUP})
